<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>PCF.Domain-Notation</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Markup">\begin{code}</a>
<a id="14" class="Symbol">{-#</a> <a id="18" class="Keyword">OPTIONS</a> <a id="26" class="Pragma">--rewriting</a> <a id="38" class="Pragma">--confluence-check</a> <a id="57" class="Pragma">--lossy-unification</a> <a id="77" class="Symbol">#-}</a>
<a id="81" class="Keyword">open</a> <a id="86" class="Keyword">import</a> <a id="93" href="Agda.Builtin.Equality.html" class="Module">Agda.Builtin.Equality</a>
<a id="115" class="Keyword">open</a> <a id="120" class="Keyword">import</a> <a id="127" href="Agda.Builtin.Equality.Rewrite.html" class="Module">Agda.Builtin.Equality.Rewrite</a>

<a id="158" class="Keyword">module</a> <a id="165" href="PCF.Domain-Notation.html" class="Module">PCF.Domain-Notation</a> <a id="185" class="Keyword">where</a>

<a id="192" class="Keyword">open</a> <a id="197" class="Keyword">import</a> <a id="204" href="Relation.Binary.PropositionalEquality.Core.html" class="Module">Relation.Binary.PropositionalEquality.Core</a>
  <a id="249" class="Keyword">using</a> <a id="255" class="Symbol">(</a><a id="256" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">_≡_</a><a id="259" class="Symbol">)</a> <a id="261" class="Keyword">public</a>

<a id="269" class="Comment">------------------------------------------------------------------------</a>
<a id="342" class="Comment">-- Domains</a>

<a id="354" class="Keyword">postulate</a>
  <a id="Domain"></a><a id="366" href="PCF.Domain-Notation.html#366" class="Postulate">Domain</a>  <a id="374" class="Symbol">:</a> <a id="376" href="Agda.Primitive.html#388" class="Primitive">Set₁</a>
  <a id="⟪_⟫"></a><a id="383" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪_⟫</a>     <a id="391" class="Symbol">:</a> <a id="393" href="PCF.Domain-Notation.html#366" class="Postulate">Domain</a> <a id="400" class="Symbol">→</a> <a id="402" href="Agda.Primitive.html#388" class="Primitive">Set</a>

<a id="407" class="Keyword">variable</a>
  <a id="418" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="420" href="PCF.Domain-Notation.html#420" class="Generalizable">E</a> <a id="422" class="Symbol">:</a> <a id="424" href="PCF.Domain-Notation.html#366" class="Postulate">Domain</a>
  <a id="433" href="PCF.Domain-Notation.html#433" class="Generalizable">P</a> <a id="435" class="Symbol">:</a> <a id="437" href="Agda.Primitive.html#388" class="Primitive">Set</a>
  <a id="443" href="PCF.Domain-Notation.html#443" class="Generalizable">d₁</a> <a id="446" href="PCF.Domain-Notation.html#446" class="Generalizable">d₂</a> <a id="449" class="Symbol">:</a> <a id="451" href="Agda.Primitive.html#388" class="Primitive">Set</a>

<a id="456" class="Keyword">postulate</a>
  <a id="⊥"></a><a id="468" href="PCF.Domain-Notation.html#468" class="Postulate">⊥</a> <a id="470" class="Symbol">:</a> <a id="472" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="474" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="476" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a>  <a id="479" class="Comment">-- bottom element</a>

<a id="498" class="Comment">------------------------------------------------------------------------</a>
<a id="571" class="Comment">-- Function domains</a>

<a id="592" class="Keyword">postulate</a>
  <a id="_→ᶜ_"></a><a id="604" href="PCF.Domain-Notation.html#604" class="Postulate Operator">_→ᶜ_</a>     <a id="613" class="Symbol">:</a> <a id="615" href="PCF.Domain-Notation.html#366" class="Postulate">Domain</a> <a id="622" class="Symbol">→</a> <a id="624" href="PCF.Domain-Notation.html#366" class="Postulate">Domain</a> <a id="631" class="Symbol">→</a> <a id="633" href="PCF.Domain-Notation.html#366" class="Postulate">Domain</a>      <a id="645" class="Comment">-- assume continuous</a>
  <a id="_→ˢ_"></a><a id="668" href="PCF.Domain-Notation.html#668" class="Postulate Operator">_→ˢ_</a>     <a id="677" class="Symbol">:</a> <a id="679" href="Agda.Primitive.html#388" class="Primitive">Set</a>    <a id="686" class="Symbol">→</a> <a id="688" href="PCF.Domain-Notation.html#366" class="Postulate">Domain</a> <a id="695" class="Symbol">→</a> <a id="697" href="PCF.Domain-Notation.html#366" class="Postulate">Domain</a>      <a id="709" class="Comment">-- always continuous</a>
  <a id="dom-cts"></a><a id="732" href="PCF.Domain-Notation.html#732" class="Postulate">dom-cts</a>  <a id="741" class="Symbol">:</a> <a id="743" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="745" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="747" href="PCF.Domain-Notation.html#604" class="Postulate Operator">→ᶜ</a> <a id="750" href="PCF.Domain-Notation.html#420" class="Generalizable">E</a> <a id="752" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a> <a id="754" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="756" class="Symbol">(</a><a id="757" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="759" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="761" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a> <a id="763" class="Symbol">→</a> <a id="765" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="767" href="PCF.Domain-Notation.html#420" class="Generalizable">E</a> <a id="769" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a><a id="770" class="Symbol">)</a>
  <a id="set-cts"></a><a id="774" href="PCF.Domain-Notation.html#774" class="Postulate">set-cts</a>  <a id="783" class="Symbol">:</a> <a id="785" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="787" href="PCF.Domain-Notation.html#433" class="Generalizable">P</a> <a id="789" href="PCF.Domain-Notation.html#668" class="Postulate Operator">→ˢ</a> <a id="792" href="PCF.Domain-Notation.html#420" class="Generalizable">E</a> <a id="794" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a> <a id="796" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="798" class="Symbol">(</a><a id="799" href="PCF.Domain-Notation.html#433" class="Generalizable">P</a> <a id="801" class="Symbol">→</a> <a id="803" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="805" href="PCF.Domain-Notation.html#420" class="Generalizable">E</a> <a id="807" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a><a id="808" class="Symbol">)</a>

<a id="811" class="Symbol">{-#</a> <a id="815" class="Keyword">REWRITE</a> <a id="823" href="PCF.Domain-Notation.html#732" class="Postulate">dom-cts</a> <a id="831" href="PCF.Domain-Notation.html#774" class="Postulate">set-cts</a> <a id="839" class="Symbol">#-}</a>

<a id="844" class="Keyword">postulate</a>
  <a id="fix"></a><a id="856" href="PCF.Domain-Notation.html#856" class="Postulate">fix</a> <a id="860" class="Symbol">:</a> <a id="862" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="864" class="Symbol">(</a><a id="865" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="867" href="PCF.Domain-Notation.html#604" class="Postulate Operator">→ᶜ</a> <a id="870" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a><a id="871" class="Symbol">)</a> <a id="873" href="PCF.Domain-Notation.html#604" class="Postulate Operator">→ᶜ</a> <a id="876" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="878" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a>  <a id="881" class="Comment">-- fixed point of endofunction</a>

  <a id="915" class="Comment">-- Properties</a>
  <a id="fix-fix"></a><a id="931" href="PCF.Domain-Notation.html#931" class="Postulate">fix-fix</a> <a id="939" class="Symbol">:</a> <a id="941" class="Symbol">(</a><a id="942" href="PCF.Domain-Notation.html#942" class="Bound">f</a> <a id="944" class="Symbol">:</a> <a id="946" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="948" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="950" href="PCF.Domain-Notation.html#604" class="Postulate Operator">→ᶜ</a> <a id="953" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="955" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a><a id="956" class="Symbol">)</a> <a id="958" class="Symbol">→</a> <a id="960" href="PCF.Domain-Notation.html#856" class="Postulate">fix</a> <a id="964" href="PCF.Domain-Notation.html#942" class="Bound">f</a> <a id="966" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="968" href="PCF.Domain-Notation.html#942" class="Bound">f</a> <a id="970" class="Symbol">(</a><a id="971" href="PCF.Domain-Notation.html#856" class="Postulate">fix</a> <a id="975" href="PCF.Domain-Notation.html#942" class="Bound">f</a><a id="976" class="Symbol">)</a>

<a id="979" class="Comment">-- Flat domains</a>

<a id="996" class="Keyword">postulate</a>
  <a id="_+⊥"></a><a id="1008" href="PCF.Domain-Notation.html#1008" class="Postulate Operator">_+⊥</a>    <a id="1015" class="Symbol">:</a> <a id="1017" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1021" class="Symbol">→</a> <a id="1023" href="PCF.Domain-Notation.html#366" class="Postulate">Domain</a>               <a id="1044" class="Comment">-- lifted set</a>
  <a id="η"></a><a id="1060" href="PCF.Domain-Notation.html#1060" class="Postulate">η</a>      <a id="1067" class="Symbol">:</a> <a id="1069" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="1071" href="PCF.Domain-Notation.html#433" class="Generalizable">P</a> <a id="1073" href="PCF.Domain-Notation.html#668" class="Postulate Operator">→ˢ</a> <a id="1076" href="PCF.Domain-Notation.html#433" class="Generalizable">P</a> <a id="1078" href="PCF.Domain-Notation.html#1008" class="Postulate Operator">+⊥</a> <a id="1081" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a>              <a id="1096" class="Comment">-- inclusion</a>
  <a id="_♯"></a><a id="1111" href="PCF.Domain-Notation.html#1111" class="Postulate Operator">_♯</a>     <a id="1118" class="Symbol">:</a> <a id="1120" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="1122" class="Symbol">(</a><a id="1123" href="PCF.Domain-Notation.html#433" class="Generalizable">P</a> <a id="1125" href="PCF.Domain-Notation.html#668" class="Postulate Operator">→ˢ</a> <a id="1128" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a><a id="1129" class="Symbol">)</a> <a id="1131" href="PCF.Domain-Notation.html#604" class="Postulate Operator">→ᶜ</a> <a id="1134" href="PCF.Domain-Notation.html#433" class="Generalizable">P</a> <a id="1136" href="PCF.Domain-Notation.html#1008" class="Postulate Operator">+⊥</a> <a id="1139" href="PCF.Domain-Notation.html#604" class="Postulate Operator">→ᶜ</a> <a id="1142" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="1144" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a>  <a id="1147" class="Comment">-- Kleisli extension</a>

  <a id="1171" class="Comment">-- Properties</a>
  <a id="elim-♯-η"></a><a id="1187" href="PCF.Domain-Notation.html#1187" class="Postulate">elim-♯-η</a>  <a id="1197" class="Symbol">:</a> <a id="1199" class="Symbol">(</a><a id="1200" href="PCF.Domain-Notation.html#1200" class="Bound">f</a> <a id="1202" class="Symbol">:</a> <a id="1204" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="1206" href="PCF.Domain-Notation.html#433" class="Generalizable">P</a> <a id="1208" href="PCF.Domain-Notation.html#668" class="Postulate Operator">→ˢ</a> <a id="1211" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="1213" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a><a id="1214" class="Symbol">)</a> <a id="1216" class="Symbol">(</a><a id="1217" href="PCF.Domain-Notation.html#1217" class="Bound">p</a> <a id="1219" class="Symbol">:</a> <a id="1221" href="PCF.Domain-Notation.html#433" class="Generalizable">P</a><a id="1222" class="Symbol">)</a> <a id="1224" class="Symbol">→</a>  <a id="1227" class="Symbol">(</a><a id="1228" href="PCF.Domain-Notation.html#1200" class="Bound">f</a> <a id="1230" href="PCF.Domain-Notation.html#1111" class="Postulate Operator">♯</a><a id="1231" class="Symbol">)</a> <a id="1233" class="Symbol">(</a><a id="1234" href="PCF.Domain-Notation.html#1060" class="Postulate">η</a> <a id="1236" href="PCF.Domain-Notation.html#1217" class="Bound">p</a><a id="1237" class="Symbol">)</a>  <a id="1240" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1242" href="PCF.Domain-Notation.html#1200" class="Bound">f</a> <a id="1244" href="PCF.Domain-Notation.html#1217" class="Bound">p</a>
  <a id="elim-♯-⊥"></a><a id="1248" href="PCF.Domain-Notation.html#1248" class="Postulate">elim-♯-⊥</a>  <a id="1258" class="Symbol">:</a> <a id="1260" class="Symbol">(</a><a id="1261" href="PCF.Domain-Notation.html#1261" class="Bound">f</a> <a id="1263" class="Symbol">:</a> <a id="1265" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="1267" href="PCF.Domain-Notation.html#433" class="Generalizable">P</a> <a id="1269" href="PCF.Domain-Notation.html#668" class="Postulate Operator">→ˢ</a> <a id="1272" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="1274" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a><a id="1275" class="Symbol">)</a> <a id="1277" class="Symbol">→</a>          <a id="1288" class="Symbol">(</a><a id="1289" href="PCF.Domain-Notation.html#1261" class="Bound">f</a> <a id="1291" href="PCF.Domain-Notation.html#1111" class="Postulate Operator">♯</a><a id="1292" class="Symbol">)</a> <a id="1294" href="PCF.Domain-Notation.html#468" class="Postulate">⊥</a>      <a id="1301" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1303" href="PCF.Domain-Notation.html#468" class="Postulate">⊥</a>

<a id="1306" class="Comment">-- McCarthy conditional</a>

<a id="1331" class="Comment">-- t ⟶ d₁ , d₂ : ⟪ D ⟫  (t : Bool +⊥ ; d₁, d₂ : ⟪ D ⟫)</a>

<a id="1387" class="Keyword">open</a> <a id="1392" class="Keyword">import</a> <a id="1399" href="Data.Bool.Base.html" class="Module">Data.Bool.Base</a>
  <a id="1416" class="Keyword">using</a> <a id="1422" class="Symbol">(</a><a id="1423" href="Agda.Builtin.Bool.html#173" class="Datatype">Bool</a><a id="1427" class="Symbol">;</a> <a id="1429" href="Agda.Builtin.Bool.html#198" class="InductiveConstructor">true</a><a id="1433" class="Symbol">;</a> <a id="1435" href="Agda.Builtin.Bool.html#192" class="InductiveConstructor">false</a><a id="1440" class="Symbol">;</a> <a id="1442" href="Data.Bool.Base.html#1515" class="Function Operator">if_then_else_</a><a id="1455" class="Symbol">)</a> <a id="1457" class="Keyword">public</a>

<a id="1465" class="Keyword">postulate</a>
  <a id="_⟶_,_"></a><a id="1477" href="PCF.Domain-Notation.html#1477" class="Postulate Operator">_⟶_,_</a>  <a id="1484" class="Symbol">:</a> <a id="1486" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="1488" href="Agda.Builtin.Bool.html#173" class="Datatype">Bool</a> <a id="1493" href="PCF.Domain-Notation.html#1008" class="Postulate Operator">+⊥</a> <a id="1496" href="PCF.Domain-Notation.html#604" class="Postulate Operator">→ᶜ</a> <a id="1499" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="1501" href="PCF.Domain-Notation.html#604" class="Postulate Operator">→ᶜ</a> <a id="1504" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="1506" href="PCF.Domain-Notation.html#604" class="Postulate Operator">→ᶜ</a> <a id="1509" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="1511" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a>   <a id="1515" class="Comment">-- McCarthy conditional</a>

  <a id="1542" class="Comment">-- Properties</a>
  <a id="true-cond"></a><a id="1558" href="PCF.Domain-Notation.html#1558" class="Postulate">true-cond</a>    <a id="1571" class="Symbol">:</a> <a id="1573" class="Symbol">{</a><a id="1574" href="PCF.Domain-Notation.html#1574" class="Bound">d₁</a> <a id="1577" href="PCF.Domain-Notation.html#1577" class="Bound">d₂</a> <a id="1580" class="Symbol">:</a> <a id="1582" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="1584" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="1586" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a><a id="1587" class="Symbol">}</a> <a id="1589" class="Symbol">→</a> <a id="1591" class="Symbol">(</a><a id="1592" href="PCF.Domain-Notation.html#1060" class="Postulate">η</a> <a id="1594" href="Agda.Builtin.Bool.html#198" class="InductiveConstructor">true</a> <a id="1599" href="PCF.Domain-Notation.html#1477" class="Postulate Operator">⟶</a> <a id="1601" href="PCF.Domain-Notation.html#1574" class="Bound">d₁</a> <a id="1604" href="PCF.Domain-Notation.html#1477" class="Postulate Operator">,</a> <a id="1606" href="PCF.Domain-Notation.html#1577" class="Bound">d₂</a><a id="1608" class="Symbol">)</a>  <a id="1611" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1613" href="PCF.Domain-Notation.html#1574" class="Bound">d₁</a>
  <a id="false-cond"></a><a id="1618" href="PCF.Domain-Notation.html#1618" class="Postulate">false-cond</a>   <a id="1631" class="Symbol">:</a> <a id="1633" class="Symbol">{</a><a id="1634" href="PCF.Domain-Notation.html#1634" class="Bound">d₁</a> <a id="1637" href="PCF.Domain-Notation.html#1637" class="Bound">d₂</a> <a id="1640" class="Symbol">:</a> <a id="1642" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="1644" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="1646" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a><a id="1647" class="Symbol">}</a> <a id="1649" class="Symbol">→</a> <a id="1651" class="Symbol">(</a><a id="1652" href="PCF.Domain-Notation.html#1060" class="Postulate">η</a> <a id="1654" href="Agda.Builtin.Bool.html#192" class="InductiveConstructor">false</a> <a id="1660" href="PCF.Domain-Notation.html#1477" class="Postulate Operator">⟶</a> <a id="1662" href="PCF.Domain-Notation.html#1634" class="Bound">d₁</a> <a id="1665" href="PCF.Domain-Notation.html#1477" class="Postulate Operator">,</a> <a id="1667" href="PCF.Domain-Notation.html#1637" class="Bound">d₂</a><a id="1669" class="Symbol">)</a> <a id="1671" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1673" href="PCF.Domain-Notation.html#1637" class="Bound">d₂</a>
  <a id="bottom-cond"></a><a id="1678" href="PCF.Domain-Notation.html#1678" class="Postulate">bottom-cond</a>  <a id="1691" class="Symbol">:</a> <a id="1693" class="Symbol">{</a><a id="1694" href="PCF.Domain-Notation.html#1694" class="Bound">d₁</a> <a id="1697" href="PCF.Domain-Notation.html#1697" class="Bound">d₂</a> <a id="1700" class="Symbol">:</a> <a id="1702" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟪</a> <a id="1704" href="PCF.Domain-Notation.html#418" class="Generalizable">D</a> <a id="1706" href="PCF.Domain-Notation.html#383" class="Postulate Operator">⟫</a><a id="1707" class="Symbol">}</a> <a id="1709" class="Symbol">→</a> <a id="1711" class="Symbol">(</a><a id="1712" href="PCF.Domain-Notation.html#468" class="Postulate">⊥</a> <a id="1714" href="PCF.Domain-Notation.html#1477" class="Postulate Operator">⟶</a> <a id="1716" href="PCF.Domain-Notation.html#1694" class="Bound">d₁</a> <a id="1719" href="PCF.Domain-Notation.html#1477" class="Postulate Operator">,</a> <a id="1721" href="PCF.Domain-Notation.html#1697" class="Bound">d₂</a><a id="1723" class="Symbol">)</a>       <a id="1731" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">≡</a> <a id="1733" href="PCF.Domain-Notation.html#468" class="Postulate">⊥</a>

<a id="1736" class="Keyword">infixr</a> <a id="1743" class="Number">0</a>  <a id="1746" href="PCF.Domain-Notation.html#604" class="Postulate Operator">_→ᶜ_</a>
<a id="1751" class="Keyword">infixr</a> <a id="1758" class="Number">0</a>  <a id="1761" href="PCF.Domain-Notation.html#668" class="Postulate Operator">_→ˢ_</a>
<a id="1766" class="Keyword">infix</a>  <a id="1773" class="Number">10</a>   <a id="1778" href="PCF.Domain-Notation.html#1008" class="Postulate Operator">_+⊥</a>
<a id="1782" class="Keyword">infixr</a> <a id="1789" class="Number">20</a>  <a id="1793" href="PCF.Domain-Notation.html#1477" class="Postulate Operator">_⟶_,_</a>
<a id="1799" class="Markup">\end{code}</a></pre></body></html>